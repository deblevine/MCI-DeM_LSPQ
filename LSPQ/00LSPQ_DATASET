# delimit; 
clear all; 
set more off; 
set maxvar 32767;
capture log close;
set seed 517228;

/* 
////////////////////////////////////////////////////////////////////////////////
\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
***************************** ANALYTIC INFORMATION *****************************
MCI DeM Aim 2, Study 3: Tx preferences 
Deb Levine, MD MPH, University of Michigan
Created by Rachael Whitney, PhD: 02/06/2020
Updated by Rachael Whitney, PhD: 05/21/2020

STUDY AIM. Determine the influence of MCI on patient and study partner preferences 
for AMI and ischemic stroke treatment. STUDY HYPOTHESIS. MCI patients and study 
partners prefer less intensive treatment than patients with no MCI. 

Analyses in this syntax file employ the data from the 2020 master file freeze and 
follow the plan outlined in "MCI DeM_Study 3_Analytic Plan_RTW200102.docx". Variables
used in these analyses are detailed in the analytic plan. General note: variables 
end in suffixes that provide information about respondent type (patient or partner) 
and the person being referenced (self or dyad-mate):

	Variable contains patient information provided by the patient: _pt
	Variable contains patient information provided by the partner: _apt		
	Variable contains partner information provided by the partner: _ptr
	Variable contains dyad information provided by the partner: _dyad

---FILES IN USE---
INPUT, 2020 MASTER:	    MCIDeM_AIM2_STUDY3_MASTER_200206.dta
OUTPUT, LSPQ ANALYTIC:	MCIDeM_AIM2_STUDY3_LSPQ_`date'.dta
********************************************************************************
\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
////////////////////////////////////////////////////////////////////////////////
*/

**************************************************************** PROGRAM MACROS;
*** DIRECTORY;
local cd "S:\Intmed_Rsrch2\GenMed\Restricted\COG-HSR PROJECTS\2020-MCIDeM-PROJECTS\AIM2_S3-SURVEY\RW_S3-SURV-STROBE_prj";
*** DATE;
local date "200831";
*** IN PATH;
local inpath = "S:\Intmed_Rsrch2\GenMed\Restricted\MASTER DATASETS\Data_Projects\2020_MCIDeM-S3-Survey\dataout\MCIDeM_AIM2_STUDY3_MASTER_200206.dta";
*** OUT PATH;
local outpath = "`cd'\dataout\MCIDeM_AIM2_STUDY3_LSPQ_RTW`date'.dta";

*************************************************************** DATA MANAGEMENT;
***** DATA FILES;
*MASTER;
use "`inpath'", clear;
*TEMP;
tempfile master;
tempfile strobe;
save `strobe';

***** EXCLUSIONS;
quietly{;
drop if cog_assessment_12mo_pt!=1;
keep if age_pt>=65 & age_pt!=.;
keep if englishspeaking_pt==1;
keep if mci_status_pt!=.;
keep if complete_dyad==1;
drop if race_pt==.; 
*drop if age_ptr==.;
/* INCLUSION CRITERIA
1. Patient diagnosis of MCI or no MCI
2. Patient age >=65 years
3. Patient race White or Black
4. Patient received cog testing within 12 months of baseline
5. Patient reads and speaks English
6. Patient has a study partner age >=18 that reads and speaks English, knows the
   patient well and can answer questions about them regarding medical treatment
*/
};

***** ALTERED VARIABLES;
quietly {;
label define educ 1 "No college" 2 "Some college" 3 "4-Year degree" 4 "Graduate degree";
label define maritalstatu 0 "Single" 1 "Married/partner";
label define depression 0 "No indication" 1 "Some indication";
label define adls 0 "No difficulty" 1 "Difficulty";
label define relationship 1 "Spouse" 2 "Child" 3 "Friend" 4 "Other";
label define freq 1 "Daily" 2 "Several times/week" 3 "Once/week or less";
label define site 0 "DUDL" 1 "UMDL";
local type "pt ptr";
foreach t in `type' {;

*** EDUCATION;
recode education_`t' (1 2 3 = 1) (4 5 = 2) (6 = 3) (7 = 4);
label values education_`t' educ;

*** MARITAL STATUS;
gen marital_statu2_`t' = marital_statu_`t';
recode marital_statu2_`t' (1 2 = 1) (3 4 5 = 0);
label values marital_statu2_`t' maritalstatu;

*** PHQ-2;
recode phq2_`t' (1 2 3 4 5 6 = 1);
label values phq2_`t' depression;

*** ADLS;
recode adl_`t' (1 2 3 4 5 6 = 1);
label values adl_`t' adls;
};

*** SITE;
gen x = site=="UMDL";
drop site;
rename x site;
label values site site;

*** DYAD RELATIONSHIP; 
gen relationship_dyad2 = relationship_dyad;
recode relationship_dyad2 (1 4 5 = 7);
recode relationship_dyad2 (2=1) (3=2) (6=3) (7=4);
label values relationship_dyad2 relationship;

*** FREQ SEE DYAD;
gen freq_see_dyad2 = freq_see_dyad; 
recode freq_see_dyad2 (2=1) (3=2) (4 5 6=3);
label values freq_see_dyad2 freq;

*** FREQ SPEAK DYAD;
gen freq_speak_dyad2 = freq_speak_dyad; 
recode freq_speak_dyad2 (4 5=3);
label values freq_speak_dyad2 freq;

*** LSPQ REVERSE CODING;
gen lspq_reverse_pt = 24-lspq_pt;
gen lspq_reverse_apt = 24-lspq_apt;

*** LSPQ ZERO INFLATION;
tempfile master;
save `master';
tempfile lspq;
*TOTAL;
gen zero_pt = lspq_reverse_pt==0;
gen zero_apt = lspq_reverse_apt==0;
*SCENARIO RECODES;
local plspq "ch_antibiotics_* ch_cpr_* ch_gallbladder_* ch_artificial_fd_* emph_antibiotics_* emph_cpr_*  emph_gallbladder_* emph_artificial_fd_* stroke_antibiotics_* stroke_cpr_* stroke_gallbladder_* stroke_artificial_fd_* cancer_antibiotics_* cancer_cpr_* cancer_gallbladder_* cancer_artificial_fd_* hattack_antibiotics_* hattack_cpr_* hattack_gallbladder_* hattack_artificial_fd_* ad_antibiotics_* ad_cpr_* ad_gallbladder_* ad_artificial_fd_*";
label define pref 1 "Want treatment" 0 "Don't want treatment";
foreach p in `plspq' {;
recode `p' (1 2 3 = 1) (4 5 = 0);
label values `p' pref;
};
*CURRENT HEALTH;
egen zero_ch_pt = rowtotal(ch_antibiotics_pt ch_cpr_pt ch_gallbladder_pt ch_artificial_fd_pt), missing;
egen zero_ch_apt = rowtotal(ch_antibiotics_apt ch_cpr_apt ch_gallbladder_apt ch_artificial_fd_apt), missing;
recode zero_ch_* (1 2 3 = 0) (4 = 1);
*EMPHYSEMA;
egen zero_emph_pt = rowtotal(emph_antibiotics_pt emph_cpr_pt  emph_gallbladder_pt emph_artificial_fd_pt), missing;
egen zero_emph_apt = rowtotal(emph_antibiotics_apt emph_cpr_apt  emph_gallbladder_apt emph_artificial_fd_apt), missing;
recode zero_emph_* (1 2 3 = 0) (4 = 1);
*STROKE;
egen zero_stroke_pt = rowtotal(stroke_antibiotics_pt stroke_cpr_pt stroke_gallbladder_pt stroke_artificial_fd_pt), missing;
egen zero_stroke_apt = rowtotal(stroke_antibiotics_apt stroke_cpr_apt stroke_gallbladder_apt stroke_artificial_fd_apt), missing;
recode zero_stroke_* (1 2 3 = 0) (4 = 1);
*COLON CANCER;
egen zero_cancer_pt = rowtotal(cancer_antibiotics_pt cancer_cpr_pt cancer_gallbladder_pt cancer_artificial_fd_pt), missing;
egen zero_cancer_apt = rowtotal(cancer_antibiotics_apt cancer_cpr_apt cancer_gallbladder_apt cancer_artificial_fd_apt), missing;
recode zero_cancer_* (1 2 3 = 0) (4 = 1);
*HEART ATTACK; 
egen zero_hattack_pt = rowtotal(hattack_antibiotics_pt hattack_cpr_pt hattack_gallbladder_pt hattack_artificial_fd_pt), missing;
egen zero_hattack_apt = rowtotal(hattack_antibiotics_apt hattack_cpr_apt hattack_gallbladder_apt hattack_artificial_fd_apt), missing;
recode zero_hattack_* (1 2 3 = 0) (4 = 1);
*ALZHEIMERS;
egen zero_ad_pt = rowtotal(ad_antibiotics_pt ad_cpr_pt ad_gallbladder_pt ad_artificial_fd_pt), missing;
egen zero_ad_apt = rowtotal(ad_antibiotics_apt ad_cpr_apt ad_gallbladder_apt ad_artificial_fd_apt), missing;
recode zero_ad_* (1 2 3 = 0) (4 = 1);
keep dyadid zero_*;
save `lspq';
use `master';
merge 1:1 dyadid using `lspq', nogenerate;

*** DSRS QUARTILE;
summ dsrs_apt, detail;
gen dsrs_quartile_apt = 0 if dsrs_apt<=`r(p25)' & dsrs_apt!=.;
replace dsrs_quartile_apt = 1 if dsrs_quartile_apt==. & dsrs_apt<=`r(p50)' & dsrs_apt!=.;
replace dsrs_quartile_apt = 2 if dsrs_quartile_apt==. & dsrs_apt<=`r(p75)' & dsrs_apt!=.;
replace dsrs_quartile_apt = 3 if dsrs_quartile_apt==. & dsrs_apt!=.;
drop dsrs;

*** SDM PREFERENCES;
local person "_pt _apt";
foreach p in `person'{;
*AMI;
egen x = rowtotal(angioplasty`p' surgery`p' heart_rehab`p' cholesterol_med`p') if angioplasty`p'!=. & surgery`p'!=. & heart_rehab`p'!=. & cholesterol_med`p';
gen ami_sdm`p' = x-4;
drop x;
*STROKE;
egen x = rowtotal(clotbusting_med`p' sx_on_neck_artery`p' stroke_rehab`p' blood_thinning_med`p') if clotbusting_med`p'!=. & sx_on_neck_artery`p'!=. & stroke_rehab`p'!=. & blood_thinning_med`p';
gen stk_sdm`p' = x-4;
drop x;
};

*** TIME FROM COG TEST TO SURVEY;
gen cogtosurvey_pt = survey_date_pt-cogtest_date_pt if site==1;
};

*** PREFERENCES;
local var "decision_pt angioplasty_pt angioplasty_apt surgery_pt surgery_apt heart_rehab_pt heart_rehab_apt cholesterol_med_pt cholesterol_med_apt clotbusting_med_pt clotbusting_med_apt sx_on_neck_artery_pt sx_on_neck_artery_apt stroke_rehab_pt stroke_rehab_apt blood_thinning_med_pt blood_thinning_med_apt";

foreach v in `var'{;
gen `v'2 = `v';
recode `v'2 (4 5 = 0) (1 2 3 = 1);
tab `v' `v'2;
};


************************************************************** ANALYTIC DATASET;
save "`outpath'", replace;
